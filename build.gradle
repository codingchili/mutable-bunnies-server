plugins {
    id "java"
}

repositories {
    jcenter()
}

project.version = "1.3.5"
project.group = 'com.github.codingchili.mutable'

subprojects {
    plugins.apply("java")

    version = rootProject.version
    group = rootProject.group

    sourceCompatibility = 11
    targetCompatibility = 11

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    sourceSets {
        main {
            java {
                srcDir 'main/java'
            }
            resources {
                srcDir 'main/resources'
            }
        }
        test {
            java {
                srcDir 'test/java'
            }
            resources {
                srcDir 'test/resources'
            }
        }
    }

    sourceSets {
        main.java.srcDirs += 'main/java'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://jitpack.io' }
        jcenter()
    }

    test {
        testLogging {
            exceptionFormat "full"
        }
        reports.html.enabled = true
    }
}

task testReport(type: TestReport, dependsOn: 'build') {
    destinationDir = file("$buildDir/reports/allTests")
    reportOn subprojects*.test
}

task archiveTestReport(type: Zip, dependsOn: testReport) {
    archiveBaseName = 'testreport'
    from fileTree(file("$buildDir/reports/allTests"))
}

jar.enabled = false